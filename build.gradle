import org.yaml.snakeyaml.Yaml

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
  }
}

plugins {
  id 'org.openapi.generator' version '5.4.0'
  id 'java'
  id 'maven-publish'
  id 'net.researchgate.release' version '2.8.1'
  id "com.rameshkp.openapi-merger-gradle-plugin" version "1.0.4"
}

project.ext {
  name = 'Passende FinanzierungsVorschläge API'
  artifactId = 'baufi-passende-vorschlaege-api'
  groupId = 'de.europace'
  version = "${version}"
}

group = 'de.europace'

repositories {
  mavenCentral()
}

def apiVersion = "${property("version")}".toString()

publishing {
  publications {
    mavenJava(MavenPublication) {
      version = apiVersion
      artifact "$rootDir/api/baufi-passende-vorschlaege-api.yaml"
    }
  }
}

def apiFileName = "$rootDir/api/sources/${property("baufi.passende.vorschlaege.api.file.name")}"
def apiConfig = new Yaml().load( new File(apiFileName).newInputStream() )

task assertVersions() {
  doLast {
    def openApiConfigVersion = apiConfig.info.version
    def releaseArtifactVersion = "${version}"
    if (openApiConfigVersion != releaseArtifactVersion) {
      throw new BuildCancelledException("Mismatching API and artifact versions. API version: '$openApiConfigVersion' gradle artifact version: '$releaseArtifactVersion'")
    }
  }
}

openApiValidate {
  inputSpec.set apiFileName
  recommend.set true
}


openApiGenerate {
  generatorName.set 'javascript'
  inputSpec.set 'api/baufi-passende-vorschlaege-api.yaml'
  outputDir.set "$buildDir/generated".toString()
  apiPackage.set 'de.europace.baufi.passende.vorschlaege.api'
  modelPackage.set 'de.hypoport.ep2.baufismart.passende.vorschlaege.model'
}

release {
  failOnCommitNeeded = false
  git {
    requireBranch = 'main'
    pushToRemote = 'origin'
  }
}

openApiMerger {
  inputDirectory.set(file("./api/sources"))

  output {
    directory.set(new File("api"))
    fileName.set("baufi-passende-vorschlaege-api")
  }

  openApi {
    openApiVersion.set("3.0.3")
    info {
      title.set("Empfohlene Finanzierungsvorschläge API")
      description.set("API zum Abruf und Merken passender empfohlener Finanzierungsvorschläge")
      version.set(apiVersion)
      contact {
        name.set("Europace AG")
        url.set("https://www.europace2.de")
        email.set("passt@europace.de")
      }
    }
    servers {
      register("production") {
        url.set("https://baufismart.api.europace.de")
        description.set("Production server")
      }
    }
  }
}

tasks.openApiValidate.dependsOn tasks.assertVersions
publish.dependsOn tasks.openApiValidate

afterReleaseBuild.dependsOn publish
